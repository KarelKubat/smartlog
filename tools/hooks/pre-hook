#!/usr/bin/env perl
use strict;
use File::Basename;
use File::Spec;

# Purpose: run some sanity checks before `git` actions.

my %checks_to_run = (
    'pre-push'   => [ qw(allcommitted) ],
    'pre-commit' => [ qw(auxfiles gotests) ],
);
my $orgpath = File::Spec->rel2abs(__FILE__);
my $hook = $orgpath;
$hook =~ s{.*/}{};

my @hooks_to_run;
if ($hook eq 'pre-hook') {
    @hooks_to_run = keys(%checks_to_run);
} else {
    @hooks_to_run = ($hook);
}

for $hook (@hooks_to_run) {
    my @checks = @{$checks_to_run{$hook}}
        or die("misconfiguration in $orgpath: no checks configured for hook $hook\n");

    for my $check (@checks) {
        my $cmd = "tools/checks/$check";
        system($cmd) and die("$hook hook failed on $cmd\n");
    }
}